module test;
using test::*;
using system::*;

class Calculator : StateMachine
{
	var valueFirst : string = "";
	var op : string = "";
	prop Value : string = "0" {}
	
	func Update(value : string) : void
	{
		SetValue(value);
		valueFirst = value;
	}
	
	func Calculate() : void
	{
		if (valueFirst == "")
		{
			valueFirst = Value;
		}
		else if (op == "+")
		{
			Update((cast double valueFirst) + (cast double Value));
		}
		else if (op == "*")
		{
			Update((cast double valueFirst) * (cast double Value));
		}
		else
		{
			raise $"Unrecognized operator: $(op)";
		}
	}
	
	$state_machine
	{
		$state_input Digit(i : int);
		$state_input Dot();
		$state_input Add();
		$state_input Mul();
		$state_input Equal();
		$state_input Clear();
	
		$state Digits()
		{
			$switch (continue)
			{
				case Digit(i):
				{
					Value = Value & i;
					$goto_state Digits();
				}
			}
		}

		$state Integer(newNumber: bool)
		{
			$switch (continue)
			{
				case Digit(i):
				{
					if (newNumber)
					{
						Value = i;
					}
					else
					{
						Value = Value & i;
					}
					$goto_state Digits();
				}
			}
		}

		$state Number()
		{
			$push_state Integer(true);
			$switch (continue, return)
			{
				case Dot():
				{
					Value = Value & ".";
				}
			}
			$push_state Integer(false);
		}

		$state Operator()
		{
			$push_state Number();
			$switch (raise)
			{
				case Add():		{Calculate(); op = "+";}
				case Mul():		{Calculate(); op = "-";}
				case Equal():	{Calculate(); op = "=";}
				case Clear():
				{
					valueFirst = "";
					op = "";
					Value = "0";
				}
			}
			$goto_state Operator();
		}
		
		$state default()
		{
			$goto_state Operator();
		}
	}
}

var s = "";

func main():string
{
	var c = new Calculator^();
	s=$"[$(c.Value)]";
	attach(c.ValueChanged, func():void
	{
		s=$"$(s)[$(c.Value)]";
	});

	c.Digit(1);
	c.Dot();
	c.Digit(5);
	c.Add();
	c.Digit(2);
	c.Digit(1);
	c.Dot();
	c.Digit(2);
	c.Digit(5);
	c.Mul();
	c.Digit(2);
	c.Equal();
	c.Clear();

	return s;
}